subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse-wtp'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
      api 'org.slf4j:slf4j-api:1.7.21'
      api 'com.google.inject:guice:4.0'

      implementation 'org.apache.logging.log4j:log4j-api:2.7'
      implementation 'org.apache.logging.log4j:log4j-core:2.7'
      implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.7'


      testImplementation 'junit:junit:4.12'
      testImplementation 'org.hamcrest:hamcrest-core:1.3'
    }

    jar {
        manifest {
            attributes 'Implementation-Title': project.name, 
                    'Implementation-Version': project.version,
                    'Built-By': System.getProperty('user.name'),
                    'Build-Time': new Date().format('yyyy-MM-dd HH:mm:ss.SSS'),
                    'Built-JDK': System.getProperty('java.version')
        }
    }           

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                            
                artifact sourcesJar
                artifact javadocJar
            }       
        }
    }    

}
